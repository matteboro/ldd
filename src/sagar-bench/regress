#!/bin/tcsh

if ($1 == "--noPreds") then
  echo "===== no predicate abstraction ====="
  set BASECMD="./diamond --branch 5 --depth 5 --repeat 25 --vars 5"
  foreach I ("" "--qelim2" "--oct" "--oct --octCons" "--tvpi" "--tvpi --tvpiCons")
    foreach J ("" "--summary" "--image")
      foreach K ("" "--eagerElim")
        foreach L ("" "--unsat")
          foreach M ("" "--compInv")
            set CMD="$BASECMD $I $J $K $L $M"
            echo $CMD
            $CMD |& grep ERROR &
          end
        end
        wait
      end
    end
  end
else if ($1 == "--preds") then
  echo "===== predicate abstraction ====="
  set BASECMD="./diamond --repeat 30"
  foreach I ("--branch 5 --depth 5 --vars 5 --preds 2" \
             "--oct --branch 2 --depth 3 --vars 2 --preds 2" \
             "--oct --octCons --branch 2 --depth 3 --vars 2 --preds 2" \
             "--tvpi --branch 2 --depth 3 --vars 2 --preds 2" \
             "--tvpi --tvpiCons --branch 2 --depth 2 --vars 2 --preds 2")
    foreach J ("--summary" "--image")
      foreach K ("" "--eagerElim")
        foreach L ("" "--unsat")
          foreach M ("" "--compInv")
            set CMD="$BASECMD $I $J $K $L $M"
            echo $CMD
            $CMD |& grep ERROR &
          end
        end
        wait
      end
    end
  end
else
  $0 "--noPreds" |& grep -v "Done\|Exit\|\[[0-9]\]"
  $0 "--preds" |& grep -v "Done\|Exit\|\[[0-9]\]"
endif
